# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/av_codec/config.gni")

config("av_codec_service_config") {
  visibility = [ ":*" ]

  cflags = [
    "-std=c++17",
    "-fno-rtti",
    "-fno-exceptions",
    "-Wall",
    "-fno-common",
    "-fstack-protector-strong",
    "-Wshadow",
    "-FPIC",
    "-FS",
    "-O2",
    "-D_FORTIFY_SOURCE=2",
    "-fvisibility=hidden",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wdate-time",
    "-Werror",
    "-Wextra",
    "-Wimplicit-fallthrough",
    "-Wsign-compare",
    "-Wunused-parameter",
  ]

  # include_dirs = [
  #   "codeclist/client",
  #   "codec/server/include",
  #   "source/server/include",
  #   "$av_codec_root_dir/services/include",
  #   "$av_codec_root_dir/services/services/codec",
  #   "$av_codec_root_dir/services/services/source",
  #   "$av_codec_root_dir/services/dfx/include",
  #   "$av_codec_root_dir/services/engine/codeclist",
  #   "$av_codec_root_dir/interfaces/inner_api/native",
  #   "$av_codec_root_dir/interfaces/kits/c",
  # ]

  include_dirs = [
    "$av_codec_root_dir/frameworks/native/avcodec/",
    "$av_codec_root_dir/frameworks/native/avcodeclist/",
    "$av_codec_root_dir/frameworks/native/avdemuxer/",
    "$av_codec_root_dir/frameworks/native/avmuxer/",
    "$av_codec_root_dir/frameworks/native/avsource/",
    "$av_codec_root_dir/frameworks/native/capi/common/",
    "$av_codec_root_dir/interfaces/inner_api/native/",
    "$av_codec_root_dir/interfaces/kits/c/",
    "$av_codec_root_dir/services/dfx/include/",
    "$av_codec_root_dir/services/engine/codeclist/",
    "$av_codec_root_dir/services/engine/base/include/",
    "$av_codec_root_dir/services/engine/muxer/",
    "$av_codec_root_dir/services/engine/plugin/core/",
    "$av_codec_root_dir/services/engine/plugin/interface/",
    "$av_codec_root_dir/services/engine/plugin/plugins/demuxer/ffmpeg_demuxer/",
    "$av_codec_root_dir/services/engine/plugin/plugins/muxer/ffmpeg_muxer/",
    "$av_codec_root_dir/services/include/",
    "$av_codec_root_dir/services/services/codeclist/client/",
    "$av_codec_root_dir/services/services/codeclist/ipc/",
    "$av_codec_root_dir/services/services/codeclist/server/",
    "$av_codec_root_dir/services/services/codec/client/",
    "$av_codec_root_dir/services/services/codec/ipc/",
    "$av_codec_root_dir/services/services/codec/server/",
    "$av_codec_root_dir/services/services/common/",
    "$av_codec_root_dir/services/services/demuxer/client/include/",
    "$av_codec_root_dir/services/services/demuxer/ipc/include/",
    "$av_codec_root_dir/services/services/demuxer/server/include/",
    "$av_codec_root_dir/services/services/factory/",
    "$av_codec_root_dir/services/services/muxer/client/",
    "$av_codec_root_dir/services/services/muxer/ipc/",
    "$av_codec_root_dir/services/services/muxer/server/",
    "$av_codec_root_dir/services/services/sa_avcodec/client/",
    "$av_codec_root_dir/services/services/sa_avcodec/ipc/",
    "$av_codec_root_dir/services/services/sa_avcodec/server/include/",
    "$av_codec_root_dir/services/services/source/client/",
    "$av_codec_root_dir/services/services/source/ipc/",
    "$av_codec_root_dir/services/services/source/server/",
    "$av_codec_root_dir/services/utils/include/",
    # "$av_codec_root_dir/services/services/codecbase/",
    # "$av_codec_root_dir/services/services/codecbase/server/",
    # "$av_codec_root_dir/services/services/codecbase/server/include/",
  ]

  defines = []
  defines += av_codec_defines

  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  if (target_cpu == "arm64") {
    av_codec_plugin_path = "\"/system/lib64/media/av_codec_plugins\""
  } else {
    av_codec_plugin_path = "\"/system/lib/media/av_codec_plugins\""
  }

  defines += [
    "AV_CODEC_PLUGIN_PATH=${av_codec_plugin_path}",
    "AV_CODEC_PLUGIN_FILE_TAIL=\".z.so\"",
  ]
}

ohos_shared_library("av_codec_service") {
  install_enable = true

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  
  configs = [
    ":av_codec_service_config",
    "$av_codec_root_dir/services/dfx:av_codec_service_log_dfx_public_config",
  ]

  sources = [
    "$av_codec_root_dir/frameworks/native/common/avcodec_errors.cpp",
    "common/avsharedmemory_ipc.cpp",
    "sa_avcodec/ipc/avcodec_listener_proxy.cpp",
    "sa_avcodec/ipc/avcodec_parcel.cpp",
    "sa_avcodec/ipc/avcodec_service_stub.cpp",
    "sa_avcodec/server/avcodec_server.cpp",
    "sa_avcodec/server/avcodec_server_manager.cpp",
    "$av_codec_root_dir/services/engine/muxer/muxer_engine_impl.cpp",
    "$av_codec_root_dir/services/engine/plugin/core/muxer_factory.cpp",
    "$av_codec_root_dir/services/engine/plugin/core/muxer.cpp",
    "$av_codec_root_dir/services/engine/plugin/core/plugin_loader.cpp",
  ]


  if (multimedia_av_codec_support_codec) {
    sources += [
      "codec/ipc/codec_listener_proxy.cpp",
      "codec/ipc/codec_service_stub.cpp",
      "codec/server/codec_server.cpp",
      "$av_codec_root_dir/services/engine/base/codecbase.cpp",
    ]
  }
  if (multimedia_av_codec_support_codeclist) {
    sources += [
      "codeclist/ipc/codeclist_service_stub.cpp",
      "codeclist/server/codeclist_server.cpp",
      "sa_avcodec/ipc/codeclist_parcel.cpp",
    ]
  }
  if (multimedia_av_codec_support_muxer) {
    sources += [
      "muxer/ipc/muxer_service_stub.cpp",
      "muxer/server/muxer_server.cpp",
    ]
  }


  deps = [
    "$av_codec_root_dir/services/dfx:av_codec_service_dfx",
    "$av_codec_root_dir/services/utils:av_codec_format",
    "$av_codec_root_dir/services/utils:av_codec_service_utils",
    "$av_codec_root_dir/services/engine:av_codec_engine_package",
  ]

  if (multimedia_av_codec_support_codeclist) {
    deps += [
      "$av_codec_root_dir/services/engine/codeclist:av_codec_engine_codeclist",
    ]
  }

  external_deps = [
    "c_utils:utils",
    "ipc:ipc_single",
    "hiviewdfx_hilog_native:libhilog",
    "safwk:system_ability_fwk",
    "graphic_standard:surface",
  ]

  subsystem_name = "multimedia"
  part_name = "av_codec"
}
