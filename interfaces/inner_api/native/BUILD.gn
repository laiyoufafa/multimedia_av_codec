# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/av_codec/config.gni")

config("av_codec_packege_include") {
  include_dirs = [
    "$av_codec_root_dir/frameworks/native/avcodec",
    "$av_codec_root_dir/frameworks/native/avcodeclist",
    "$av_codec_root_dir/frameworks/native/avdemuxer",
    "$av_codec_root_dir/frameworks/native/avmuxer",
    "$av_codec_root_dir/frameworks/native/avsource",
    "$av_codec_root_dir/interfaces/inner_api/native",
    "$av_codec_root_dir/interfaces/kits/c",
    "$av_codec_root_dir/services/dfx/include",
    "$av_codec_root_dir/services/include",
    "$av_codec_root_dir/services/services/codec/client",
    "$av_codec_root_dir/services/services/codec/ipc",
    "$av_codec_root_dir/services/services/codeclist/client",
    "$av_codec_root_dir/services/services/codeclist/ipc",
    "$av_codec_root_dir/services/services/common",
    "$av_codec_root_dir/services/services/demuxer/client",
    "$av_codec_root_dir/services/services/demuxer/ipc",
    "$av_codec_root_dir/services/services/muxer/client",
    "$av_codec_root_dir/services/services/muxer/ipc",
    "$av_codec_root_dir/services/services/sa_avcodec/client",
    "$av_codec_root_dir/services/services/sa_avcodec/ipc",
    "$av_codec_root_dir/services/services/source/client",
    "$av_codec_root_dir/services/services/source/ipc",
    "$av_codec_root_dir/services/utils/include",
  ]
}

config("av_codec_client_local_config") {
  include_dirs = [
    "$av_codec_root_dir/services/services/factory",
    "$av_codec_root_dir/services/engine/factory",

    # "$av_codec_root_dir/services/engine/muxer",
    # "$av_codec_root_dir/services/engine/plugin/common",
    # "$av_codec_root_dir/services/engine/plugin/core",
    # "$av_codec_root_dir/services/engine/plugin/interface",
  ]
}

config("av_codec_client_ipc_config") {
  include_dirs = [
    "$av_codec_root_dir/services/services/codec/client",
    "$av_codec_root_dir/services/services/codec/ipc",
    "$av_codec_root_dir/services/services/codeclist/client",
    "$av_codec_root_dir/services/services/codeclist/ipc",
    "$av_codec_root_dir/services/services/common",
    "$av_codec_root_dir/services/services/demuxer/client",
    "$av_codec_root_dir/services/services/demuxer/ipc",
    "$av_codec_root_dir/services/services/muxer/client",
    "$av_codec_root_dir/services/services/muxer/ipc",
    "$av_codec_root_dir/services/services/sa_avcodec/client",
    "$av_codec_root_dir/services/services/sa_avcodec/ipc",
    "$av_codec_root_dir/services/services/source/client",
    "$av_codec_root_dir/services/services/source/ipc",
  ]
}

config("av_codec_client_public_config") {
  include_dirs = [
    "./",
    "$av_codec_root_dir/frameworks/native/avcodec",
    "$av_codec_root_dir/frameworks/native/avcodeclist",
    "$av_codec_root_dir/frameworks/native/avdemuxer",
    "$av_codec_root_dir/frameworks/native/avmuxer",
    "$av_codec_root_dir/frameworks/native/avsource",
    "$av_codec_root_dir/interfaces/kits/c",
    "$av_codec_root_dir/services/include",
    "$av_codec_root_dir/services/dfx/include",
    "$av_codec_root_dir/services/utils/include",

    # "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    # "//utils/system/safwk/native/include",
    # "//third_party/bounds_checking_function/include",
    # "//commonlibrary/c_utils/base/include",
  ]

  cflags = [
    "-fno-exceptions",
    "-Wall",
    "-fno-common",
    "-fstack-protector-strong",
    "-Wshadow",
    "-FPIC",
    "-FS",
    "-O2",
    "-D_FORTIFY_SOURCE=2",
    "-Wformat=2",
    "-Wdate-time",
  ]

  cflags_cc = [
    "-std=c++17",
    "-fno-rtti",
  ]

  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  defines = []
  defines += av_codec_defines

  if (target_cpu == "arm64") {
    av_codec_plugin_path = "\"/system/lib64/media/av_codec_plugins\""
  } else {
    av_codec_plugin_path = "\"/system/lib/media/av_codec_plugins\""
  }

  defines += [
    "AV_CODEC_PLUGIN_PATH=${av_codec_plugin_path}",
    "AV_CODEC_PLUGIN_FILE_TAIL=\".z.so\"",
  ]
}

ohos_shared_library("av_codec_client") {
  install_enable = true
  public_configs = [
    ":av_codec_client_local_config",
    ":av_codec_client_ipc_config",
    ":av_codec_client_public_config",
    ":av_codec_packege_include",
  ]

  configs = [ ":av_codec_packege_include" ]

  sources = [
    #      "$av_codec_root_dir/frameworks/native/avsource/avsource_impl.cpp",
    "$av_codec_root_dir/frameworks/native/common/avcodec_errors.cpp",
    "$av_codec_root_dir/services/services/common/avsharedmemory_ipc.cpp",
    "$av_codec_root_dir/services/services/sa_avcodec/client/avcodec_client.cpp",
    "$av_codec_root_dir/services/services/sa_avcodec/ipc/avcodec_listener_stub.cpp",
    "$av_codec_root_dir/services/services/sa_avcodec/ipc/avcodec_parcel.cpp",
    "$av_codec_root_dir/services/services/sa_avcodec/ipc/avcodec_service_proxy.cpp",
  ]
  if (multimedia_av_codec_support_codec) {
    sources += [
      "$av_codec_root_dir/frameworks/native/avcodec/avcodec_audio_decoder_impl.cpp",
      "$av_codec_root_dir/frameworks/native/avcodec/avcodec_audio_encoder_impl.cpp",
      "$av_codec_root_dir/frameworks/native/avcodec/avcodec_video_decoder_impl.cpp",
      "$av_codec_root_dir/frameworks/native/avcodec/avcodec_video_encoder_impl.cpp",
      "$av_codec_root_dir/services/services/codec/client/codec_client.cpp",
      "$av_codec_root_dir/services/services/codec/ipc/codec_listener_stub.cpp",
      "$av_codec_root_dir/services/services/codec/ipc/codec_service_proxy.cpp",
    ]
  }
  if (multimedia_av_codec_support_codeclist) {
    sources += [
      "$av_codec_root_dir/frameworks/native/avcodeclist/avcodeclist_impl.cpp",
      "$av_codec_root_dir/services/services/codeclist/client/codeclist_client.cpp",
      "$av_codec_root_dir/services/services/codeclist/ipc/codeclist_service_proxy.cpp",
      "$av_codec_root_dir/services/services/sa_avcodec/ipc/codeclist_parcel.cpp",
    ]
  }
  if (multimedia_av_codec_support_demuxer) {
    sources += [
      "$av_codec_root_dir/frameworks/native/avdemuxer/avdemuxer_impl.cpp",
      "$av_codec_root_dir/services/services/demuxer/client/demuxer_client.cpp",
      "$av_codec_root_dir/services/services/demuxer/ipc/demuxer_service_proxy.cpp",
    ]
  }
  if (multimedia_av_codec_support_source) {
    sources += [
      "$av_codec_root_dir/frameworks/native/avsource/avsource_impl.cpp",
      "$av_codec_root_dir/services/services/source/client/source_client.cpp",
      "$av_codec_root_dir/services/services/source/ipc/source_service_proxy.cpp",
    ]
  }
  if (multimedia_av_codec_support_muxer) {
    sources += [
      "$av_codec_root_dir/frameworks/native/avmuxer/avmuxer_impl.cpp",
      "$av_codec_root_dir/services/services/muxer/client/muxer_client.cpp",
      "$av_codec_root_dir/services/services/muxer/ipc/muxer_service_proxy.cpp",
    ]
  }

  deps = [
    "$av_codec_root_dir/services/dfx:av_codec_service_dfx",
    "$av_codec_root_dir/services/utils:av_codec_format",
    "//third_party/bounds_checking_function:libsec_static",
  ]

  if (multimedia_av_codec_support_codeclist) {
    deps += [ "$av_codec_root_dir/services/engine:av_codec_engine_package" ]
  }

  external_deps = [
    "c_utils:utils",
    "graphic_standard:surface",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "multimedia"
  part_name = "av_codec"
}
